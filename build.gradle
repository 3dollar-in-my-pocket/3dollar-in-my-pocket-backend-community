buildscript {
    ext {
        // Spring
        springBootVersion = "3.4.0"
        springDependencyManagementVersion = "1.1.7"

        // Swagger
        springDocVersion = "2.8.3"
        findBugsJsr305Version = "3.0.2"
    }
}

plugins {
    id "org.springframework.boot" version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id "application"
}

allprojects {
    group = "com.threedollar"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply {
        plugin "idea"
        plugin "java-library"
        plugin "org.springframework.boot"
        plugin "io.spring.dependency-management"
        plugin "java-test-fixtures"
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        // SpringBoot Validation
        implementation("org.springframework.boot:spring-boot-starter-validation")

        // Json
        implementation("org.springframework.boot:spring-boot-starter-json")
        implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")

        // SpringBoot Configuration Processor
        implementation("org.springframework.boot:spring-boot-configuration-processor")
        annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

        // SpringBoot Test
        testImplementation("org.springframework.boot:spring-boot-starter-test")

        // Lombok
        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")
        testCompileOnly("org.projectlombok:lombok")
        testAnnotationProcessor("org.projectlombok:lombok")
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        testLogging {
            showExceptions = true
            exceptionFormat = 'FULL'
            showCauses = true
            events = ['FAILED']
        }
    }
}
